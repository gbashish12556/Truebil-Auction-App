apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        test {
            storeFile file("auction-app-test-keystore.keystore")
            storePassword "android"
            keyAlias "auction-app-debug-prod-alias"
            keyPassword "android"
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.truebil.business"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 31
        versionName "2.2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testBuildType System.getProperty('testBuildType', 'debug')
    }

    buildTypes {

        debug {
            buildConfigField 'String', 'API_PATH', '"https://api.oldhonk.co.in/auction_app"'
            buildConfigField 'String', 'API_PATH_BASE', '"https://api.oldhonk.co.in"'
            buildConfigField 'String', 'WEBSITE_LINK', '"https://pwa.oldhonk.co.in"'

            resValue "string", "app_name", "T.A. Debug-Dev"
            applicationIdSuffix ".debug.dev"
        }

        debugProd {
            buildConfigField 'String', 'API_PATH', '"https://api.truebil.com/auction_app"'
            buildConfigField 'String', 'API_PATH_BASE', '"https://api.truebil.com"'
            buildConfigField 'String', 'WEBSITE_LINK', '"https://m.truebil.com"'

            resValue "string", "app_name", "T.A. Debug-Prod"
            applicationIdSuffix ".debug.prod"

            debuggable true
            signingConfig signingConfigs.test
        }

        release {
            buildConfigField 'String', 'API_PATH', '"https://api.truebil.com/auction_app"'
            buildConfigField 'String', 'API_PATH_BASE', '"https://api.truebil.com"'
            buildConfigField 'String', 'WEBSITE_LINK', '"https://m.truebil.com"'

            resValue "string", "app_name", "Truebil for Business"

            minifyEnabled true
            //shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildToolsVersion '28.0.3'

    applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == "release") {
                outputFileName = "auction-app-${variant.buildType.name}-v${defaultConfig.versionCode}.apk"
            }
        }
    }
}

dependencies {
    // Support libraries
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'

    // Third-party, Firebase
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation 'com.google.firebase:firebase-database:16.0.5'
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-config:16.1.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.github.instacart.truetime-android:library:3.4'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'

    // Testing-only dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
}
apply plugin: 'com.google.gms.google-services'